import "@cadl-lang/rest";
import "@azure-tools/cadl-autorest";
import "@cadl-lang/openapi";
import "@azure-tools/cadl-azure-core";

using Cadl.Http;
using Cadl.Rest;
using Azure.Core;
using Azure.Core.Foundations.Extensions;
using OpenAPI;
using Autorest;
using ADP.DataManagement.Classifications;

@tag("DataStreamClassifications")
namespace ADP.DataManagement.Measurements.DataStreams.Classifications {
  @client(Clients.DataStreamClassificationsClient)
  interface DataStreamClassifications {
    @doc("Get classification by schema name.")
    get is ResourceRead<DataStreamClassification>;

    #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
    @doc("Assigns the classification to the data-stream.")
    @extension("x-ms-long-running-operation", true)
    @asyncOperationOptions("location")
    @pollingOperation(ADP.Common.Operations.LongRunningOperationStatusLocation)
    create is CustomLongRunningResourceCreateUsingHttpPost<
      DataStreamClassification,
      CustomBodyWrapper<DataStreamClassificationCreationParameters>
    >;

    #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
    @doc("Unassign the classification from the data-stream.")
    @extension("x-ms-long-running-operation", true)
    @asyncOperationOptions("location")
    @pollingOperation(ADP.Common.Operations.LongRunningOperationStatusLocation)
    delete is CustomLongRunningResourceDelete<DataStreamClassification>;

    #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
    @doc("Lists the classifications assigned to the data-stream.")
    list is CustomResourceList<DataStreamClassification>;
  }
}
